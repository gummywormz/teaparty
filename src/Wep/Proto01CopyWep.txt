actor Proto01CopyWep : Weapon
{
Weapon.AmmoUse 1
Weapon.AmmoGive 28
Weapon.SlotNumber 1
scale 2.0
Obituary "%o was bombed by %k's \chProto \cmBuster\c-."
Inventory.Pickupmessage "You got \chProto\cmman\c-'s shield!"
weapon.ammotype "Proto01CopyWepAmmo"
+WEAPON.AMMO_OPTIONAL
+WEAPON.DONTBOB
States
{
Spawn:
TNT1 A 1
stop
Ready:
BUST B 0 A_JumpIfInventory("Twice",1,"Ready2")
BUST B 0 ACS_ExecuteAlways(991,0,200)
BUST B 0 A_GiveInventory("Twice",1)
Goto Ready2+2
Ready2:
PRO1 Q 0 ACS_ExecuteAlways(998,0,200)
PRO1 Q 0 A_SpawnItemEx("Proto01ShieldX",23,-17,8,momx, momy, momz, 0, SXF_ABSOLUTEMOMENTUM, 0)
PRO1 Q 0 A_SpawnItemEx("Proto01ShieldFX",23,-17,8,momx, momy, momz, 0, SXF_ABSOLUTEMOMENTUM, 0)
PRO1 Q 0 A_JumpIfNoAmmo("NoAmmo")
PRO1 Q 1 A_WeaponReady
Goto Ready2+1
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
PRO1 Q 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
PRO1 Q 1 A_Raise
Loop
Fire:
PRO1 S 3 A_JumpIfNoAmmo("NoAmmo")
PRO1 Q 0 A_PlaySoundEx("weapon/mbuster","Weapon")
PRO1 Q 0 A_FireCustomMissile("MegaShot",0,0,8,0)
PRO1 T 3
PRO1 U 2
PRO1 U 0 A_Refire
PRO1 R 2
goto Ready2+1
NoAmmo:
TNT1 A 0 //ACS_Execute(979,0)
TNT1 A 1 A_PlaySoundEx("weapon/centaurflash","Weapon")
TNT1 A 1 A_SpawnItemEx("BrokenShield01",23,-17,8,random(-5,5), random(-2,2), random(5,17), 0, SXF_NOCHECKPOSITION, 0)
TNT1 A 1 A_SpawnItemEx("BrokenShield01",23,-17,8,random(-5,5), random(-2,2), random(18,29), 0, SXF_NOCHECKPOSITION, 0)
TNT1 A 1 A_GiveInventory("MegaBuster",1)
TNT1 A 1 A_SelectWeapon("MegaBuster")
TNT1 A 1 A_TakeInventory("Proto01CopyWep",1)
Goto Ready
}
}

actor Proto01CopyWepAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
}

actor Proto01ShieldX : ProtoShieldX
{
States
{
Spawn:
TNT1 A 0
TNT1 A 2
stop
Death:
TNT1 A 0 //A_PlaySound("item/protoreflect")
TNT1 A 1
stop
Pain:
TNT1 A 0 
TNT1 A 0 A_PlaySound("item/protoreflect")
TNT1 A 1 A_TakeFromTarget("Proto01CopyWepAmmo",1)
stop
Pain.Cutter:
Pain.Ice:
Pain.Thunder:
Pain.Fire:
Pain.Metal:
Pain.Air:
Pain.Bubble:
Pain.Crash:
Pain.Leaf:
Pain.Needle:
Pain.Shadow:
Pain.Magnet:
Pain.Gemini:
Pain.Ring:
Pain.Dust:
Pain.Drill:
Pain.Rain:
Pain.Kick:
Pain.Star:
Pain.Gyro:
Pain.Tomahawk:
Pain.Flame:
Pain.Wind:
Pain.Knight:
Pain.Yamato:
Pain.FCrack:
Pain.Junk:
Pain.Noise:
Pain.Burst:
Pain.Scorch:
Pain.Bolt:
Pain.Mirror:
Pain.Screw:
Pain.Flare:
TNT1 A 0 
TNT1 A 0 A_PlaySound("item/protoreflect")
TNT1 A 1 A_TakeFromTarget("Proto01CopyWepAmmo",2)
Pain.Guts:
Pain.Pharoah:
Pain.Stone:
Pain.Napalm:
Pain.Cracker:
Pain.Proto3:
TNT1 A 0 
TNT1 A 0 A_PlaySound("item/protoreflect")
TNT1 A 1 A_TakeFromTarget("Proto01CopyWepAmmo",3)
Pain.Hard:
Pain.Atomic:
Pain.Bomb:
Pain.Beam:
TNT1 A 0 
TNT1 A 0 A_PlaySound("item/protoreflect")
TNT1 A 1 A_TakeFromTarget("Proto01CopyWepAmmo",4)
stop
}
}

actor Proto01ShieldFX : ProtoShieldFX
{
States
{
Spawn:
TNT1 A 0
PR1S A 2
stop
}
}

actor BrokenShield01
{
PROJECTILE
-NOGRAVITY
Speed 1
Damage (0)
radius 2
height 5
scale 2.5
States
{
Spawn:
PRS1 C 350
stop
Death:
MMFX ABC 1
stop
}
}

actor BrokenShield02
{
PROJECTILE
-NOGRAVITY
Speed 1
Damage (0)
radius 2
height 5
scale 2.5
States
{
Spawn:
PRS1 D 350
stop
Death:
MMFX ABC 1
stop
}
}


actor Proto01CopyWepGiver : CustomInventory
{
Inventory.Pickupmessage "You got \chProto\cmman\c-'s Shield!"
inventory.pickupsound "weapon/weaponup"
scale 2.5
states
{
Spawn:
PR1S B 1
loop
Pickup:
PR1S B 0 A_JumpIfInventory("Proto01CopyWep", 1, "Failer")
PR1S B 0 A_JumpIfInventory("CanCopyWeapons", 1, "Give")
stop
Give:
PR1S B 0 A_TakeInventory("MegaBuster")
PR1S B 1 A_GiveInventory("Proto01CopyWep")
PR1S B 0 A_SelectWeapon("Proto01CopyWep")
PR1S B 0 A_TakeInventory("BassBuster",1)
PR1S B 0 A_TakeInventory("SuperadaptorWep",1)
PR1S B 0 A_TakeInventory("LaserBuster",1)
PR1S B 0 A_TakeInventory("ArrowBuster",1)
PR1S B 0 A_TakeInventory("DuoFist",1)
PR1S B 0 A_TakeInventory("ProtoBuster",1)
stop
Failer:
PR1S B 0
fail
}
}

actor Twice : Inventory //duplicate of once but with a different name
{
}